import org.redlance.dima_dencep.gradle.publish2discord.tasks.PublishDiscordTask
import org.apache.commons.lang3.StringUtils

plugins {
    id "org.redlance.dima_dencep.gradle.PublishToDiscord" version "1.0.4"
    id "com.gradleup.shadow" version "9.2.2" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.13-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base.archivesName = "${rootProject.archives_base_name}${StringUtils.capitalize(project.name)}"
    version = "${rootProject.mod_version}+mc.${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
        mavenLocal()
        // Fix issue with lwjgl-freetype not being found on macOS
        maven {
            url = "https://libraries.minecraft.net"
            content {
                includeModule("org.lwjgl", "lwjgl-freetype")
            }
        }
        maven {
            name = "NeoForged"
            url = "https://maven.neoforged.net/releases"
        }
        maven {
            name = "RedlanceMinecraft"
            url = "https://repo.redlance.org/public"
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set 21
    }

    jar {
        if (!rootProject.version.toString().contains("dev") && !project.gradle.startParameter.taskNames.contains("runClient"))
            processResources.exclude('assets/bendable_cuboids/player_animations/*')
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                name = "RedlanceMinecraft"
                url = "https://repo.redlance.org/public"
                credentials {
                    username = "dima_dencep"
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

tasks.register("publishToDiscord", PublishDiscordTask) {
    url.set(providers.environmentVariable("DISCORD_WEBHOOK"))
    username.set("BendableCuboids Updates")
    avatar.set("https://raw.githubusercontent.com/PlayerAnimationLibrary/BendableCuboids/refs/heads/main/common/src/main/resources/assets/bendable_cuboids/icon.png")

    container {
        it.title = "## âœ¨ ${rootProject.archives_base_name}\n### v${version} is out!"
        it.description = "### Changes:\n${rootProject.file("CHANGELOG.md").getText("UTF-8")}"

        it.thumbnail("https://raw.githubusercontent.com/PlayerAnimationLibrary/BendableCuboids/refs/heads/main/common/src/main/resources/assets/bendable_cuboids/icon.png")
        it.timestamp(System.currentTimeMillis())
    }

    links {
        it.fromEnvHack()
    }

    footer = "||<@&1411332102942752798>||"
}
